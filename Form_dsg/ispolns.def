Класс Перо
  перо_опущено
  проц Init()
    перо_опущено := .л.
    растяжение := .л.
    ширина := 12
    высота := 21
  кон проц
  проц Перемещение(икс,игрик)
    слева  := икс
    сверху := игрик - высота
  кон проц
  проц Сместиться_на_вектор(икс,игрик)
    видимость := .л.
    если перо_опущено
      линия(слева,сверху,слева + икс, сверху + игрик + высота,clBlack)
    всё
    слева  := слева  + икс
    сверху := сверху + игрик 
    видимость := .и.
  кон проц
  проц Опустить_перо()
    картинка := 'options/pic/p1.bmp' 
    перо_опущено := .и.
  кон проц
  проц Поднять_перо()
    картинка := 'options/pic/p2.bmp' 
    перо_опущено := .л.
  кон проц
Кон Класс
// ----------------------------------------------------------------------------

класс Аппарат
скорость,закрас
проц Init()
  ширина := 32
  высота := 32
  цвет   := 0
  закрас := .л. 
  растяжение := .л.
  видимость  := .и.
  слева  := 4
  сверху := 4
  картинка := 'options/pic/roc_v.bmp'
  скорость := 10
  для и от 1 до 40*20 шаг ширина + 8
    линия(и,0,и,800,clBlack)
  кц
  для и от 1 до 40*20  шаг высота + 8
    линия(0,и,800,и,clBlack)
  кц
кон проц
проц Закраска()
  закрас := не закрас
кон проц
проц Идти_Вниз()
  картинка := 'options/pic/roc_n.bmp'
  если закрас
    закатать()
  всё 
  буф := сверху+высота + 8
  для и от сверху до сверху+высота + 8 шаг скорость
    сверху := и
  кц  
  сверху := буф
кон проц
проц Идти_Вверх()
  картинка := 'options/pic/roc_v.bmp'
  если закрас
    закатать()
  всё 
  буф := сверху-высота - 8
  для и от сверху до сверху-высота - 8 шаг -скорость
    сверху := и
  кц
  сверху := буф
кон проц
проц Идти_Влево()
  картинка := 'options/pic/roc_l.bmp'
  если закрас
    закатать()
  всё 
  буф := слева-ширина - 8
  для и от слева до слева-ширина-8 шаг -скорость
    слева := и
  кц
  слева := буф
кон проц
проц Идти_Вправо()
  картинка := 'options/pic/roc_r.bmp'
  если закрас
    закатать()
  всё 
  буф := слева+ширина+8
  для и от слева до слева+ширина+8 шаг скорость
    слева := и
  кц
  слева := буф
кон проц
проц Прибавить_газ()
  скорость := скорость+10
кон проц
проц Убавить_газ()
  если скорость > 10 
    скорость := скорость - 10
  всё
кон проц
проц Переместиться_в(х,у)
  // Клетки с нуля
  если х = 0
    икс_доп := 1
  иначе
    икс_доп := 0
  всё
  если у = 0
    игрик_доп := 1
  иначе
    игрик_доп := 0
  всё
  слева  := 40*х + икс_доп + 4
  сверху := 40*у + игрик_доп + 4
кон проц
проц Закатать()
  залить(слева,сверху,слева+ширина,сверху+высота, clBlack)
кон проц  
кон класс

